apply plugin: 'java'

group = 'com.digitalocean.gocd'
version = '1.1.8'

// these values that go into plugin.xml
project.ext.pluginDesc = [
        id         : 'com.digitalocean.gocd.webhook',
        version    : project.version,
        goCdVersion: '16.2.1',
        name       : 'Webhook notification plugin',
        description: 'Webhook notification plugin',
        vendorName : 'DigitalOcean',
        vendorUrl  : 'https://github.com/digitalocean/gocd-webhook-notification-plugin'
]

// Force 1.7 compatibility to ensure that it works with older JVMs
sourceCompatibility = 1.7
targetCompatibility = 1.7

// In this section you declare where to find the dependencies of your project
repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
}

dependencies {
    compileOnly group: 'cd.go.plugin', name: 'go-plugin-api', version: project.pluginDesc.goCdVersion
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.8.1'
    compile group: 'commons-io', name: 'commons-io', version: '2.6'
    compile group: 'org.json', name: 'json', version: '20190722'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '3.1.0'
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '2.1'
    testCompile group: 'cd.go.plugin', name: 'go-plugin-api', version: project.pluginDesc.goCdVersion
}

processResources {
    from("src/main/resource-templates") {
        filesMatching('plugin.xml') {
            expand project.pluginDesc
        }

        filesMatching('plugin.properties') {
            expand project.pluginDesc
        }
    }
}

jar {
    from(configurations.compile) {
        into "lib/"
    }
}
